constants = require('../constants/todo_constants')

TodoActions =

  # Creates a new todo to database.
  # Waits for data because we need a new id generated by database.
  # Dispatches ADD_TODO on successful Ajax.
  addTodo:    (content) ->
    $.ajax
      method: "POST"
      url:    "/todos/"
      data:   todo:
                content: content
    .done (data, textStatus, XHR) =>
      new_todo =
        id:        data.todo.id
        content:   data.todo.content
        completed: data.todo.completed
      @dispatch(constants.ADD_TODO, new_todo: new_todo)
      $.growl.notice title: "Todo added", message: data.todo.content
    .fail (XHR, textStatus, errorThrown) =>
      $.growl.error title: "Error", message: "Error adding todo"
      console.error("#{XHR.status}: #{textStatus}: #{errorThrown}")

  # Saves a new completion status to database.
  toggleTodo: (todo, completed) ->
    @dispatch(constants.TOGGLE_TODO, todo: todo, completed: completed)
    $.ajax
      method: "PATCH"
      url:    "/todos/" + todo.id
      data:   todo:
                completed: completed
    .done (data, textStatus, XHR) =>
      title = if data.todo.completed then "Completed" else "Not completed"
      $.growl.notice title: title, message: data.todo.content
    .fail (XHR, textStatus, errorThrown) =>
      $.growl.error title: "Error", message: "Error toggleing todo completion"
      console.error("#{XHR.status}: #{textStatus}: #{errorThrown}")

  # Saves a new content to database.
  updateTodo: (todo, new_content) ->
    @dispatch(constants.UPDATE_TODO, todo: todo, new_content: new_content)
    $.ajax
      method: "PATCH"
      url:    "/todos/" + todo.id
      data:   todo:
                content: new_content
    .done (data, textStatus, XHR) =>
      $.growl.notice title: "Todo updated", message: ""
    .fail (XHR, textStatus, errorThrown) =>
      $.growl.error title: "Error", message: "Error updating todo"
      console.error("#{XHR.status}: #{textStatus}: #{errorThrown}")

  # Deletes a todo to database.
  deleteTodo: (todo) ->
    @dispatch(constants.DELETE_TODO, todo: todo)
    $.ajax
      method: "DELETE"
      url:    "/todos/" + todo.id
    .done (data, textStatus, XHR) =>
      $.growl.notice title: "Deleted", message: data.todo.content
    .fail (XHR, textStatus, errorThrown) =>
      $.growl.error title: "Error", message: "Error deleting todos"
      console.error("#{XHR.status}: #{textStatus}: #{errorThrown}")

module.exports = TodoActions
