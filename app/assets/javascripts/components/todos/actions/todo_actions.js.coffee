constants = require('../constants/todo_constants')

TodoActions =

  # Creates a new todo to database.
  # Let's wait for data because we need a new id generated by database.
  # Dispatches ADD_TODO on successful Ajax.
  addTodo:    (content) ->
    params = todo:
               content: content
    $.ajax
      method: "POST"
      url:    "/todos/"
      data:   params
    .done (data, textStatus, jqXHR) =>
      new_todo =
        id:        data.id
        content:   data.content
        completed: data.completed
      @dispatch(constants.ADD_TODO, new_todo: new_todo)
      $.growl.notice title: "", message: "Todo added"
    .fail (jqXHR, textStatus, errorThrown) =>
      $.growl.error title: "Error", message: "Error adding todo"
      console.error textStatus, errorThrown.toString()

  # Saves a new completion status to database.
  toggleTodo: (id, completed) ->
    @dispatch(constants.TOGGLE_TODO, id: id, completed: completed)
    params = todo:
               completed: completed
    $.ajax
      method: "PATCH"
      url:    "/todos/" + id
      data:   params
    .done (data, textStatus, jqXHR) =>
      title = if data.completed then "Completed" else "Not completed"
      $.growl.notice title: title, message: data.content
    .fail (jqXHR, textStatus, errorThrown) =>
      $.growl.error title: "Error", message: "Error toggleing todo completion"
      console.error textStatus, errorThrown.toString()

  # Saves a new content to database.
  updateTodo: (id, new_content) ->
    @dispatch(constants.UPDATE_TODO, id: id, new_content: new_content)
    params = todo:
               content: new_content
    $.ajax
      method: "PATCH"
      url:    "/todos/" + id
      data:   params
    .done (data, textStatus, jqXHR) =>
      $.growl.notice title: "", message: "Todo updated"
    .fail (jqXHR, textStatus, errorThrown) =>
      $.growl.error title: "Error", message: "Error updating todo"
      console.error textStatus, errorThrown.toString()

  # Deletes a todo to database. Dispatches DELETE_TODO on successful Ajax.
  deleteTodo: (id) ->
    @dispatch(constants.DELETE_TODO, id: id)
    $.ajax
      method: "DELETE"
      url:    "/todos/" + id
    .done (data, textStatus, jqXHR) =>
      $.growl.notice title: "Deleted", message: data.content
    .fail (jqXHR, textStatus, errorThrown) =>
      $.growl.error title: "Error", message: "Error deleting todos"
      console.error textStatus, errorThrown.toString()

module.exports = TodoActions
