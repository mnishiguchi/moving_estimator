constants = require('../constants/todo_constants')

TodoActions =

  # Creates a new todo to database.
  # Waits for data because we need a new id generated by database.
  # Dispatches ADD_TODO on successful Ajax.
  addTodo:    (content) ->
    $.ajax
      method: "POST"
      url:    "/todos/"
      data:   todo:
                content: content
    .done (data, textStatus, jqXHR) =>
      console.log data
      new_todo =
        id:        data.todo.id
        content:   data.todo.content
        completed: data.todo.completed
      @dispatch(constants.ADD_TODO, new_todo: new_todo)
      $.growl.notice title: "Todo added", message: data.todo.content
    .fail (jqXHR, textStatus, errorThrown) =>
      $.growl.error title: "Error", message: "Error adding todo"
      console.error textStatus, errorThrown.toString()

  # Saves a new completion status to database.
  toggleTodo: (id, completed) ->
    @dispatch(constants.TOGGLE_TODO, id: id, completed: completed)
    $.ajax
      method: "PATCH"
      url:    "/todos/" + id
      data:   todo:
                completed: completed
    .done (data, textStatus, jqXHR) =>
      title = if data.todo.completed then "Completed" else "Not completed"
      $.growl.notice title: title, message: data.todo.content
    .fail (jqXHR, textStatus, errorThrown) =>
      $.growl.error title: "Error", message: "Error toggleing todo completion"
      console.error textStatus, errorThrown.toString()

  # Saves a new content to database.
  updateTodo: (id, new_content) ->
    @dispatch(constants.UPDATE_TODO, id: id, new_content: new_content)
    $.ajax
      method: "PATCH"
      url:    "/todos/" + id
      data:   todo:
                content: new_content
    .done (data, textStatus, jqXHR) =>
      $.growl.notice title: "Todo updated", message: ""
    .fail (jqXHR, textStatus, errorThrown) =>
      $.growl.error title: "Error", message: "Error updating todo"
      console.error textStatus, errorThrown.toString()

  # Deletes a todo to database.
  deleteTodo: (id) ->
    @dispatch(constants.DELETE_TODO, id: id)
    $.ajax
      method: "DELETE"
      url:    "/todos/" + id
    .done (data, textStatus, jqXHR) =>
      $.growl.notice title: "Deleted", message: data.todo.content
    .fail (jqXHR, textStatus, errorThrown) =>
      $.growl.error title: "Error", message: "Error deleting todos"
      console.error textStatus, errorThrown.toString()

module.exports = TodoActions
