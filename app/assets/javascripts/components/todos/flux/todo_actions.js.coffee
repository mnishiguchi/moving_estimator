@TodoActions =

  # Creates a new todo to database.
  # Waits for data because we need a new id generated by database.
  # Dispatches ADD_TODO on successful Ajax.
  addTodo:    (content) ->
    $.ajax
      method: "POST"
      url:    "/todos/"
      data:   todo:
                content: content
    .done (data, textStatus, XHR) =>
      new_todo =
        id:        data.id
        content:   data.content
        completed: data.completed
      @dispatch(TodoConstants.ADD_TODO, new_todo: new_todo)
      $.growl.notice title: "Todo added", message: data.content
    .fail (XHR, textStatus, errorThrown) =>
      if error_messages = JSON.parse(XHR.responseText)
        for k, v of error_messages
          $.growl.error title: "#{ @capitalize(k) } #{v}", message: ""
      else
        $.growl.error title: "Error adding todo", message: "#{errorThrown}"
      console.error("#{textStatus}: #{errorThrown}")

  # Saves a new completion status to database.
  toggleTodo: (todo, completed) ->
    @dispatch(TodoConstants.TOGGLE_TODO, todo: todo, completed: completed)
    $.ajax
      method: "PATCH"
      url:    "/todos/" + todo.id
      data:   todo:
                completed: completed
    .done (data, textStatus, XHR) =>
      title = if data.completed then "Completed" else "Not completed"
      $.growl.notice title: title, message: data.content
    .fail (XHR, textStatus, errorThrown) =>
      if error_messages = JSON.parse(XHR.responseText)
        for k, v of error_messages
          $.growl.error title: "#{ @capitalize(k) } #{v}", message: ""
      else
        $.growl.error title: "Error toggling todo", message: "#{errorThrown}"
      console.error("#{textStatus}: #{errorThrown}")

  # Saves a new content to database.
  updateTodo: (todo, new_content) ->
    @dispatch(TodoConstants.UPDATE_TODO, todo: todo, new_content: new_content)
    $.ajax
      method: "PATCH"
      url:    "/todos/" + todo.id
      data:   todo:
                content: new_content
    .done (data, textStatus, XHR) =>
      $.growl.notice title: "Todo updated", message: ""
    .fail (XHR, textStatus, errorThrown) =>
      if error_messages = JSON.parse(XHR.responseText)
        for k, v of error_messages
          $.growl.error title: "#{ @capitalize(k) } #{v}", message: ""
      else
        $.growl.error title: "Error updating todo", message: "#{errorThrown}"
      console.error("#{textStatus}: #{errorThrown}")

  # Deletes a todo to database.
  deleteTodo: (todo) ->
    @dispatch(TodoConstants.DELETE_TODO, todo: todo)
    $.ajax
      method: "DELETE"
      url:    "/todos/" + todo.id
    .done (data, textStatus, XHR) =>
      $.growl.notice title: "Deleted", message: data.content
    .fail (XHR, textStatus, errorThrown) =>
      if error_messages = JSON.parse(XHR.responseText)
        for k, v of error_messages
          $.growl.error title: "#{ @capitalize(k) } #{v}", message: ""
      else
        $.growl.error title: "Error deleting todo", message: "#{errorThrown}"
      console.error("#{textStatus}: #{errorThrown}")
